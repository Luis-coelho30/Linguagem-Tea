programa ::= unidadesPreMain funcMain unidadesPosMain

unidadesPreMain ::= declGlobal unidadesPreMain | ε

funcMain ::= "main" "(" ")" blocoStmt

unidadesPosMain ::= funcDecl unidadesPosMain | ε

declGlobal ::= type "id" declStmtSufixo

funcDecl ::= typeORtypeArray "id" "(" paramList ")" blocoStmt
paramList ::= param paramListTail | ε
paramListTail ::= "," param paramListTail | ε
param ::= type paramSufixo
paramSufixo ::= "id" | "id" "[" "]"
 
stmt ::= declStmt | ifStmt | whileStmt | forStmt | doStmt | switchStmt | assignStmt | funcCallStmt | returnStmt | blocoStmt | breakStmt | emptyStmt
stmtList ::= stmt stmtList | ε

declStmt ::= type "id" declStmtSufixo
declStmtSufixo ::= "[" "in" "]" ";" | declStmt1
declStmt1 ::= "=" expr ";" | ";"

typeORtypeArray ::= type | typeArray
typeArray ::= type "[" "]"
type ::= "int" | "float" | "double" | "char" | "boolean" 

ifStmt ::= "if" "(" expr ")" stmt elifList elsePart
elifList ::= elif elifList | ε 
elif ::= "elif" "(" expr ")" stmt
elsePart ::= "else" stmt | ε

whileStmt ::= "while" "(" expr ")" stmt

forStmt ::= "for" "(" forInit forCond ";" forUpdate ")" stmt
forInit ::= forAssign | declStmt | ";"
forAssign ::= "id" "=" expr ";"
forCond ::= expr | ε
forUpdate ::= "id" "=" exprArit | ε

doStmt ::= "do" stmt "while" "(" expr ")" ";"

switchStmt ::= "switch" "(" exprArit ")" "{" caseList defaultPart "}"
caseList ::= caseClause caseList | ε
caseClause ::= "case" exprArit ":" stmtList breakStmt
breakStmt ::= "break" ";" 
defaultPart ::= "default" ":" stmtList | ε

assignStmt ::= "id" "=" assignExpr ";"

funcCallStmt ::= "id" "(" argList ")" ";"

returnStmt ::= "return" returnExpr ";"
returnExpr ::= expr | ε 

blocoStmt ::= "{" stmtList "}"

emptyStmt ::= ";"

expr ::= assignExpr

assignExpr ::= "id" "=" assignExpr | exprLog

exprLog ::= exprRel exprLog1
exprLog1 ::= "AND" exprRel exprLog1 | "OR" exprRel exprLog1 | ε

exprRel ::= exprComp exprRel1
exprRel1 ::= "==" exprComp exprRel1 | "!=" exprComp exprRel1 | ε

exprComp ::= exprNot exprComp1
exprComp1 ::= "<" exprArit | "<=" exprArit | ">" exprArit | ">=" exprArit | ε

exprNot ::= "!" exprNot | exprArit

exprArit ::= termo exprArit1
exprArit1 ::= "+" termo exprArit1 | "-" termo exprArit1 | ε
termo ::= fator termo1
termo1 ::= "*" fator termo1 | "/" fator termo1 | ε
fator ::= "(" expr ")" | num | "id" fatorSufixo | "true" | "false" 
fatorSufixo ::= "(" argList ")" | "[" "in" "]" | ε
argList ::= expr argListTail | ε
argListTail ::= "," expr argListTail | ε

num ::= "in" | "pf"